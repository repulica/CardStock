plugins {
	id 'org.quiltmc.loom' version '0.12.+'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.0.0'
	id 'java-library'
	id 'maven-publish'
}

def targetJavaVersion = 17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/cardstock.accessWidener")
}

repositories {
	maven {
		name 'QuiltSnapshot'
		url 'https://maven.quiltmc.org/repository/release'
		content {
			includeGroup 'org.quiltmc.quilted-fabric-api'
			includeGroupByRegex 'org.quiltmc.qsl(.[a-z_]+)?'
		}
	}
	mavenLocal()
	maven { url "https://ladysnake.jfrog.io/artifactory/mods" }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings}:v2"))
	})
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

	modImplementation "org.quiltmc:qsl:${project.qsl_version}+${project.minecraft_version}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${project.qfapi_version}-${project.minecraft_version}"

	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}") { transitive = false }
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_version}") { transitive = false }
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-item:${project.cardinal_version}") { transitive = false }
	implementation include("dev.hbeck.kdl:kdl4j:0.1.0")
}

java {
	sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
	targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'

	it.options.release.set(targetJavaVersion)
}

processResources {
	inputs.property 'version', project.version

	filesMatching('quilt.mod.json') {
		expand 'version': project.version
	}
}

jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}" }
	}
}